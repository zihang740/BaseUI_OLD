//根据isModule标签动态的切换 集成/组件模式
if (isModule){
    apply plugin: 'com.android.library'
}else{
    apply plugin: 'com.android.application'
}

def androidInfo = rootProject.ext.androidInfo
def appId = rootProject.ext.appId
def link = rootProject.ext.link

android {
    compileSdkVersion androidInfo.compileSdkVersion
    buildToolsVersion androidInfo.buildToolsVersion
    
    defaultConfig {
        if (!isModule){
            applicationId appId['app']
        }
        minSdkVersion androidInfo.minSdkVersion
        targetSdkVersion androidInfo.targetSdkVersion
        versionCode androidInfo.versionCode
        versionName androidInfo.versionName
        multiDexEnabled true
        
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName : project.getName()]
            }
        }
    }
    
    buildTypes {
        release {
            minifyEnabled false //false 关闭混淆
            //proguard-android.txt为系统已经预先设置的混淆文件，proguard-rules.pro是我们自己的混淆文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }

    sourceSets{
        main{
            if(isModule){
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java.srcDirs 'src/main/java'
            }else{
                manifest.srcFile 'src/main/local/AndroidManifest.xml'
                java.srcDirs 'src/main/local/java','src/main/java'
            }
        }
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    api link['appcompatV7']
    api link['supportV4']
    api link['recyclerView']
    api link['design']//已包含 appcompat-v7、recyclerview-v7，可以移除
    //数据库框架 ActiveAndroid 这里先注释掉, 改为jar方式引用
//    api 'com.michaelpardo:activeandroid:3.1.0-SNAPSHOT'
    //zxing
    api 'com.google.zxing:core:3.3.3'
    //gson
    api 'com.google.code.gson:gson:2.8.4'
    //retrofit2
    api 'com.squareup.retrofit2:retrofit:2.4.0'
    api 'com.squareup.retrofit2:converter-gson:2.2.0'
    api 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'
    //okhttp3
    api 'com.squareup.okhttp3:okhttp:3.10.0'
    api 'com.squareup.okhttp3:logging-interceptor:3.5.0'
    //rxJava2
    api 'io.reactivex.rxjava2:rxandroid:2.0.2'
    api 'io.reactivex.rxjava2:rxjava:2.1.14-RC1'
    // 依赖以下两个库，会自动引用基础库与Android库
    // Android组件库，里面定义了例如RxAppCompatActivity、RxFragment之类的Android组件
    // 内部引用了基础库和Android库，如果使用此库则无需再重复引用
    api 'com.trello.rxlifecycle2:rxlifecycle-components:+'
    // Android使用的库，继承NaviActivity使用
    api 'com.trello.rxlifecycle2:rxlifecycle-navi:+'
    // Android使用的库，继承LifecycleActivity使用
    // 需要引入Google的仓库支持，用法和rxlifecycle-navi类似
    api 'com.trello.rxlifecycle2:rxlifecycle-android-lifecycle:+'
    // fresco
    api 'com.facebook.fresco:fresco:1.2.0'
    // fresco | 支持 GIF 动图，需要添加
    api 'com.facebook.fresco:animated-gif:1.2.0'
    // Arouter
    api 'com.alibaba:arouter-api:1.3.1'
    // 角标
    api 'q.rorbin:badgeview:1.1.3'//https://github.com/qstumn/BadgeView
    annotationProcessor 'com.alibaba:arouter-compiler:1.1.4'
    // Android APP分享微信小程序
    api 'com.tencent.mm.opensdk:wechat-sdk-android-with-mta:+'
    // 弹性布局 (需要依赖Androidx)
//    api 'com.google.android:flexbox:+'

    api 'com.zhihu.android:matisse:0.5.2-beta4' //知乎开源图片选择框架
    api 'com.github.bumptech.glide:glide:4.7.1' //知乎开源图片选择框架 相关依赖
    api 'com.squareup.picasso:picasso:2.5.2' //知乎开源图片选择框架 相关依赖
    api 'com.github.yalantis:ucrop:2.2.3' //图片剪裁框架
    
    api files('libs/Baidu_Mtj_3.9.0.0.jar')
    api files('libs/hellocharts-library-1.5.8.jar')
    api files('libs/ActiveAndroid-3.1.0-SNAPSHOT.jar')
}

if (isModule){
    /** 以下开始是将Android Library上传到jcenter的相关配置**/
    apply plugin: 'com.novoda.bintray-release'//添加
    publish{
        repoName = 'maven'//远程仓库名字,不指明，默认是上传到maven
        userOrg = 'hzh153200037'//bintray.com的用户名
        groupId = 'com.hzh.frame'//jcenter上的路径
        artifactId = 'BaseUI'//项目名称
        publishVersion = '3.1.5'//版本号
        desc = '我的Android框架项目'//描述，不重要
        website = 'https://XXX.XXX.XXX' // 网址，自由填写
    }
    tasks.withType(Javadoc) {//防止编码问题
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('encoding', 'UTF-8')
        options.addStringOption('charSet', 'UTF-8')
    }
}